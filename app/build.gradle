apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
//apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
/*apply plugin: 'com.dicedmelon.gradle.jacoco-android'*/
apply plugin: 'com.google.gms.google-services'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
//    androidExtensions {
//        experimental = true
//    }
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.gregspitz.flashcardappkotlin"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            testCoverageEnabled true
        }
    }
    flavorDimensions "default"
    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension "default"
        }
    }
    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }
    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        unitTests {
            all {
                testLogging {
                    // Always show the result of every unit test, even if it passes.
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                }
            }
        }
    }
}

/*tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}*/

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.8.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.room:room-runtime:2.5.1"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.firebase:firebase-auth:21.1.0"
    implementation "com.google.android.gms:play-services-auth:20.4.0"
    implementation "com.google.firebase:firebase-database:20.1.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.squareup.picasso:picasso:$rootProject.picassoVersion"
    implementation "jp.wasabeef:picasso-transformations:2.4.0"
    kapt "androidx.room:room-compiler:2.5.1"

    // Flexible Adapter
    // https://github.com/davideas/FlexibleAdapter
    implementation "eu.davidea:flexible-adapter:$rootProject.flexibleAdapterVersion"
    implementation "eu.davidea:flexible-adapter-ui:$rootProject.flexibleAdapterUiVersion"
    implementation "eu.davidea:flexible-adapter-livedata:$rootProject.flexibleAdapterLiveDataVersion"
    implementation "eu.davidea:flexible-adapter-databinding:$rootProject.flexibleAdapterDataBindingVersion"

    // Uncommon maths for probability distribution
    implementation files('libs/uncommons-maths-1.2.3.jar')

    // Dagger
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.2"
    testImplementation "com.nhaarman:mockito-kotlin:$rootProject.mockitoKotlinVersion"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.5.1"
    androidTestImplementation "androidx.room:room-testing:2.5.1"
    androidTestUtil "androidx.test:orchestrator:1.4.2"
}
